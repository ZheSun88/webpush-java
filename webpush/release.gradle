publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
            pom {
                name = 'web-push'
                description = 'A Web Push library for Java.'
                url = 'https://github.com/web-push-libs/webpush-java'
                scm {
                    connection = 'scm:git:git@github.com:web-push-libs/webpush-java.git'
                    developerConnection = 'scm:git:git@github.com:web-push-libs/webpush-java.git'
                    url = 'git@github.com:web-push-libs/webpush-java.git'
                }
                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://opensource.org/licenses/MIT'
                    }
                }
                developers {
                    developer {
                        id = 'martijndwars'
                        name = 'Martijn Dwars'
                        email = 'ikben@martijndwars.nl'
                    }
                }
            }

        }
    }
    repositories {
        maven {
            credentials {
                username ossrhUsername
                password ossrhPassword
            }
            url getRepositoryUrl()
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}

def getRepositoryUrl() {
    if (version.endsWith('SNAPSHOT')) {
        return 'https://oss.sonatype.org/content/repositories/snapshots/'
    } else {
        return 'https://oss.sonatype.org/service/local/staging/deploy/maven2/'
    }
}
